apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {

  def file = new File("keystore.credentials")
  def fileExists = false
  if (file.exists()) {
    fileExists = true
  }

  Properties props = new Properties()
  if (fileExists) {
    props.load(new FileInputStream(file))
  }

  signingConfigs {
    if (fileExists) {
      release {
        storeFile new File(props['keystore'])
        storePassword props['keystore.password']
        keyAlias props['keyAlias']
        keyPassword props['keyPassword']
      }
    }
  }
  compileSdkVersion 27
  buildToolsVersion '27.0.3'
  defaultConfig {
    applicationId "wordtextcounter.details.main"
    minSdkVersion 16
    targetSdkVersion 27
    versionCode 5
    versionName "3.0.1"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary = true
  }
  lintOptions {
    abortOnError false
  }
  buildTypes {
    if (fileExists) {
      release {
        minifyEnabled true
        shrinkResources true
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        signingConfig signingConfigs.release
      }
    }
    debug {
      applicationIdSuffix ".test"
    }
  }
  compileOptions {
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }
}

ext {
  versions = [support           : "27.1.1",
              rxjava            : "2.1.13",
              rxAndroid         : "2.0.2",
              androidArchVersion: "1.1.1",
              room              : "1.1.1",
              rxbinding         : "2.1.1",
              bottombar         : "2.3.1",
              logger            : "2.1.1",
              foldingcell       : "1.2.2",
              rxrelay           : "2.0.0",
              rxkotlin          : "2.2.0",
              sdp               : "1.0.5"]

  supportDeps = [appcompat : "com.android.support:appcompat-v7:$versions.support",
                 design    : "com.android.support:design:$versions.support",
                 preference: "com.takisoft.fix:preference-v7:27.1.1.1",
                 cardview  : "com.android.support:cardview-v7:$versions.support",
                 support   : 'com.android.support:support-v4:$versions.support']

  sdpDeps = [sdp: "com.intuit.sdp:sdp-android:$versions.sdp",
             ssp: "com.intuit.ssp:ssp-android:$versions.sdp"]

  rxjavaDeps = [rxjava  : "io.reactivex.rxjava2:rxjava:$versions.rxjava",
                rxKotlin: "io.reactivex.rxjava2:rxkotlin:$versions.rxkotlin"]

  rxAndroidDeps = [rxAndroid: "io.reactivex.rxjava2:rxandroid:$versions.rxAndroid"]

  supportLibs = supportDeps.values()
  sdpLibs = sdpDeps.values()
  rxLibs = rxjavaDeps.values()
  rxAnd = rxAndroidDeps.values()
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation supportLibs
  implementation rxLibs
  implementation rxAnd
  implementation 'com.android.support.constraint:constraint-layout:1.1.2'
  testImplementation 'junit:junit:4.12'

  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

  // Lifecycle
  implementation "android.arch.lifecycle:extensions:$versions.androidArchVersion"
  kapt "android.arch.lifecycle:compiler:$versions.androidArchVersion"

  //room
  implementation "android.arch.persistence.room:runtime:$versions.room"
  kapt "android.arch.persistence.room:compiler:$versions.room"
  implementation "android.arch.persistence.room:rxjava2:$versions.room"

  //rxandroid
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

  implementation "com.roughike:bottom-bar:$versions.bottombar"

  implementation "com.jakewharton.rxbinding2:rxbinding-design:$versions.rxbinding"

  implementation "com.orhanobut:logger:$versions.logger"

  implementation 'androidx.core:core-ktx:0.3'

  implementation "com.jakewharton.rxrelay2:rxrelay:$versions.rxrelay"

  implementation 'com.google.firebase:firebase-core:16.0.1'

  implementation project(':rateus')
}

